# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_ce
pkgver=22.03.1.10
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files for Community Editions'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('7cd070b412781f92b169f352f60e7a37896808b29a14c3c54f1d0242d8626123449d836ca6ac60f239113143bca6a316caf784ce347ada65aa782d160081422a')


_caldir="$pkgname/calamares"

prepare() {
    mkdir -p "$_caldir/modules"
    mv "$_repo_name-$pkgver"            "$_repo_name"

    cp "${_repo_name}/calamares/files/netinstall-ce-base.yaml"              "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/packagechooser_ce.conf"             "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/netinstall_community_base.conf"     "$_caldir/modules/"
    cp "${_repo_name}/calamares/settings_community.conf"                    "$_caldir/"
    cp -R "${_repo_name}/calamares/images-ce"                               "$_caldir/"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$_caldir" "$pkgdir/etc/"
}
