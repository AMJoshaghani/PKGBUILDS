# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=2.3
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash yad reflector-simple systemd
  python-dbus           # for ksetwallpaper.py
  xorg-xrandr           # for eos-set-background-picture
  endeavouros-theming   # for eos-set-background-picture
)
backup=(etc/eos-script-lib-yad.conf)
url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

source=(
  $url/$pkgname
  $url/$pkgname.desktop
#  $url/${pkgname}-panel.desktop
  $url/eos-script-lib-yad
  $url/eos-script-lib-yad.conf
  $url/device-info
  $url/su-c_wrapper
  $url/eos-set-background-picture
  $url/ksetwallpaper.py
  $url/wallpaper-once
  $url/wallpaper-once.desktop
  $url/eos-kill-yad-zombies
  $url/translations-${pkgname}.bash
  $url/translations-${pkgname}-reference.bash
  $url/translations-${pkgname}-de.bash
  $url/translations-${pkgname}-en.bash
  $url/translations-${pkgname}-fi.bash
  $url/translations-${pkgname}-fr.bash
)
sha512sums=('ab0c1fa49e892583e711030afeb5aa59f1e0613866d7a5cfee8f3aa66a0ce58052d2999bfe989f2fd35ef4a73903a0a2f5c3b897f564aad4ecfc3d203ccf890b'
            'd5ad5c9fe382f1614094e6677b406c059a3b6efbb56e8c8f6dcb2aa64e0bcc158fcab1b511951d0b360b07b1a491b0c36763abbb848474f2c22f71c8ec7f15e1'
            '789d9b534cc9e121d550ab68c2c7a8f56674c25bacb3dbeaafa2d9064d7b24a6e1b5a78aa9f99ade6a299e7c7b08679f7c6fc09e89dec2d6798159d86989fe8c'
            '37385554fee75e0bd052ebf0d5abfc3808a233a430f6fc227fa03ecbc34d32fc63af72e8a9e14de5f3b4b5c03b7fdb97db1a2e1f90d24dffc2f9cf937c96e422'
            '525e80b44e04d6b17d0659c8b187811dbbf39beba6b0767ca7b76d75adec569d5e7959dfa9acc34c9a602d2dae05fb3d960954fb9b5cc9a4c6e301f4bc9acbf4'
            '4e90323a2ccddde959dd4641166bcf9cccc5cd869e79b90a8a34acb3a5a67dfeba6ab2a79a826e8e871f37f73fe634e196e75fbc972b39a35c914d9ef593eda8'
            '057bfa118a305fe7af3f8ebddcc7d1efe3d4b9bf26f61e7d3f1e756102dbfe639dea33381da363f51e1fbb12e597cbcd6ba3f43f378e3412514e2a717e92772a'
            'a3488dab4b65f215ee1513c5ff4c81b1d9e43346abb3a1e9571271c8e60e13184bd826a6206938c14c004fa5adb4f629ea832a535a51972657adb2de5bc30634'
            '4e62a6dbfac155c652e7f5fae307a75ea1648f88ec231adf03b7956ac0318306fc3c9d4def4eb42448d8a53732e2f23cf00692bbab932cee6a1ee7111e4f8d2b'
            'a5d8391ea7de40847a05a7f301871d615dbb40a6638dbb7375911ef5921cfb986298a251c251eba25ca0448dec980f05692bacab94310dd7253cf400227c80ad'
            '94395d17c28456a26edc1f4859cc382970a3a75d8c257e50721a25cceb8224a9019c43372af447f9c6800fda4f2a519e2797a4d9ad764fb736ac6e411e8d4a46'
            '36ff35de0e34439ad3d383b42ed3905f1a10c7889e645d8565b22a14c550e8c8ec6cb1b95cf99f69f4aafe4346028bb56404460cd1240ea2f95b3f573a5dc021'
            '394ac7527470178e33c655e6804c62389140bacd3dee0b8846d46c4fd6e70401e92a16182e981ef1ff7085df4d9e3d04483c7a974a435c76ff07f4134965bed7'
            '7f2b542c5854630a4b13e44cf756b93a28960230feaa705e7cf45fc754921ae32f931f561e94ca4387f3062a4c426fd506fa642ba61fd1955360df20554d7ced'
            'e5cb9f31ae8a18051833b1cb14512770558217cc5734633f2bb8bbedbac7d9cd63728e3e80f48dfb1e5c080345ca39310de52cbc2b24a15e2a61c4e6ad71de26'
            'bbcfdaf9bbce64b083d487039c7805a42a350bde6fd4a8dab98c1c016248836079146f7d4730205f229136b1565faa6a8885fe897973b0ab1660ffb6e63f2de3'
            '335de4fbe4e155ee61f0cfd0c83d47ed86bfd7c796895243f076510de33d84b30edf2d6d6b280409850bb278f0f2692bfb389b1b3a4ac8a0bce9cac0ae0816a0')

package() {
  local languages="reference de en fi fr"   # Update this list when new languages are added.
  local xx

  cd $srcdir

  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 ksetwallpaper.py         $pkgdir/usr/share/endeavouros/scripts/ksetwallpaper.py
  install -Dm644 eos-script-lib-yad       $pkgdir/usr/share/endeavouros/scripts/eos-script-lib-yad
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -Dm644 translations-${pkgname}.bash        $pkgdir/usr/share/endeavouros/scripts/translations-${pkgname}.bash
  for xx in $languages ; do
    install -Dm644 translations-${pkgname}-$xx.bash  $pkgdir/usr/share/endeavouros/scripts/translations-${pkgname}-$xx.bash
  done

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname
  install device-info                           $pkgdir/usr/bin/device-info
  install -Dm755 eos-set-background-picture     $pkgdir/usr/bin/eos-set-background-picture
  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies
  install -Dm755 su-c_wrapper                   $pkgdir/usr/bin/su-c_wrapper

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 eos-script-lib-yad.conf  $pkgdir/etc/eos-script-lib-yad.conf

  install -d $pkgdir/usr/share/applications
  install -Dm644 $pkgname.desktop         $pkgdir/usr/share/applications/$pkgname.desktop
}
