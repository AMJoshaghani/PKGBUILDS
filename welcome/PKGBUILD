# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=1.17
pkgrel=1.1
arch=('any')
license=('GPL')
depends=(
  bash yad reflector-simple systemd
  python-dbus           # for ksetwallpaper.py
  xorg-xrandr           # for eos-set-background-picture
  endeavouros-theming   # for eos-set-background-picture
)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

source=(
  $url/$pkgname
  $url/$pkgname.desktop
  $url/eos-script-lib-yad
  $url/eos-script-lib-yad.conf
  $url/device-info
  $url/eos-set-background-picture
  $url/ksetwallpaper.py
  $url/wallpaper-once
  $url/wallpaper-once.desktop
)
sha512sums=('f5063000953bb0b567939e44adb537c8012fdc5d9769dd5d9da16f0e5a90080ce7136e0adb6c7c306f1a6fae8e348cbcd7e39a91d51c5013d67be10243883f9c'
            '87a44d71b2d084aad1f9f3b66481c4cb54de147231fa0f10674e8d62904e5ec4bc096d077501794d0ea73db36f45122a84fb1f7233d260f0e4d4a37a2f85bb85'
            '30729b193eefc6dc18f210ff2cdae531351e8cc6884c435471e1cee7f6c83dca108e49e9189a066976140da16b285423107ae8c8752f485fe41fdad9b4bd768f'
            'e3313448a51744e25f368a1d18ea8a90edf43c2c1f80872848342e82df925e2e3e4d0a300ea2654fadc6b055f22cf6e38f99e58a9f0d3dd8e0f583d23c1c2994'
            '525e80b44e04d6b17d0659c8b187811dbbf39beba6b0767ca7b76d75adec569d5e7959dfa9acc34c9a602d2dae05fb3d960954fb9b5cc9a4c6e301f4bc9acbf4'
            '0af632da5b50d2125971aab83c221222819b27d69cc91436766bf8f904b3f34cb3c7ffc8ed29e2db6165224cfaca982facb0e85cb0d2a0a0aa1cec5f562ce10c'
            '618ac5e0c681da0e0b8b59fbea215bc7fde70c5ae43d717aa0b16490172a17f5e8f507b386f7b398f02b3ce57c2ae3ada156557316060e6808fce633529ebf92'
            'c51545e9a2d2d16521a3eacfa9234ead5453879183124e5e3f50a21b200c8f66ab80b49c6f2cd7fb7c663db69fbe7e508620b24eb7f26c714328d0c93e6abaa9'
            '852303566e9527739472a1badab3c8a730e64808c30f3dd6332a033fa113e1db58b771d6f39471fedffcac3a785a759319085004abe17ae6f7ad62c11d6b8417')

package() {
  cd $srcdir

  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 ksetwallpaper.py         $pkgdir/usr/share/endeavouros/scripts/ksetwallpaper.py
  install -Dm644 eos-script-lib-yad       $pkgdir/usr/share/endeavouros/scripts/eos-script-lib-yad
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname
  install device-info                           $pkgdir/usr/bin/device-info
  install -Dm755 eos-set-background-picture     $pkgdir/usr/bin/eos-set-background-picture

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 eos-script-lib-yad.conf  $pkgdir/etc/eos-script-lib-yad.conf
}
