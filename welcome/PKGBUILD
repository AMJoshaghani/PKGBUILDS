# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=3.14.11
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  systemd eos-bash-shared eos-translations
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'yad: upstream yad provider'
  'yad-eos: alternative yad provider (includes fixes)'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
)

url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

install=$pkgname.install

source=(
  $_url/$pkgname
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
  $_url/$pkgname.install
)
sha512sums=('62d5282ff9861fe43ccd86aed179b501565e1e3f5ddc232ef8cb2c888b9ea7a9588522492a8e2fd0da62fdb069c906d175e24ebae0c02a7b078872b2cc9e9d78'
            '82a1b2354f8121dc1d932035ff3ea02ad244ba5a7d11cfce2a233d31bdc04590614fae511c5e33a06553ad436cac1585201e5eb52de31c773c2346a50d43b52d'
            'cdfe56ddd154b5c7f5d237e381091b11c106f2244cd9ac3983ea40c3eae72383cfa204f737bc238cbc7e809049856ea40445f9ac3ad76b6ffa6dfc701d64eb20'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            '670aa416cb9d95a408ba258117b45d3956c58fe29a91c7069b9779440a15bcb2843e99732c82c31a5f15bfe7295c08a40dc5a064bbae4c94e6de46625284b925'
            '94254041be0757b9be01d12bd95fcaccf5ce7b1f4a7bb67cf2b39918a8ab538d6c963bc29a850244e46f7f1625ad6d25c0ea5d5c6cccfb38be2291baa6163272')
package() {
  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname

  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies
  install -Dm755 welcome-dnd                    $pkgdir/usr/bin/welcome-dnd
  install -Dm755 eos-install-mode-run-calamares $pkgdir/usr/bin/eos-install-mode-run-calamares

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop      # no --enable

  install -d $pkgdir/usr/share/applications
  cp -a $pkgname.desktop $pkgname.desktop-enable
  sed -i $pkgname.desktop-enable \
      -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
      -e 's|^#\(Exec=.* --enable.*\)$|\1|'
  install -Dm644 $pkgname.desktop-enable         $pkgdir/usr/share/applications/$pkgname.desktop      # has --enable
}
