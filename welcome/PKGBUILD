# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=3.6
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash yad systemd eos-bash-shared eos-translations
  python-dbus           # for ksetwallpaper.py
  xorg-xrandr           # for eos-set-background-picture
  endeavouros-theming   # for eos-set-background-picture
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
)
url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

# backup=(etc/eos-script-lib-yad.conf)

source=(
  $url/changelog.md
  $url/$pkgname
  $url/$pkgname.desktop
#  $url/paccache-service-manager
#  $url/eos-script-lib-yad
#  $url/eos-script-lib-yad.conf
#  $url/eos-connection-checker
#  $url/device-info
#  $url/su-c_wrapper
  $url/eos-set-background-picture
  $url/ksetwallpaper.py
  $url/wallpaper-once
  $url/wallpaper-once.desktop
  $url/eos-kill-yad-zombies
#  $url/Adding-own-commands.md
#  $url/RunInTerminal
  $url/welcome-dnd
#  $url/welcome-dnd-tips.png
#  $url/welcome-dnd-window.png
  $url/calamares-update
)
sha512sums=('c94468243f112c667a59733813888750437f5194920077ab5b824b58ebc977d4c101c836e891e1d5f2a06e57aaa67a4be8d341c98e6c741f9222f38f9d512f1d'
            '530ee7ecc66d3f8b50ab2b9a9f926a681a620104a08ccacb656e3130e0f8829869f3fb2fe950a4fc9003cf336eb58910e44d9285e4d5856d4e0d6281739bf257'
            '22ac459f10ab773ff96fed866ee4b29038452172f59bc61ce3b0b640ab9d1cb3c9b170fd1bc3838d459e11015b4f995d2ed4b695cf497eb1ddf8daa41801f267'
            '2af25af3c83fd5cf90cd9bdc15884c38432c8513801321eed437fbd173ee54fbe860e73b9e8252cfb693efc42af9d62535cff09042afc5e6a332d3a6c18b435f'
            'a3488dab4b65f215ee1513c5ff4c81b1d9e43346abb3a1e9571271c8e60e13184bd826a6206938c14c004fa5adb4f629ea832a535a51972657adb2de5bc30634'
            'abdd3bfb17ca54bff9dc57575496eb6466929a33e481a659d07b52d1a22f0a7b3b931c9fed48c9dd4b98ea25867c8cb9aa904f69eae3d531a17c73f1ab6580b1'
            'a5d8391ea7de40847a05a7f301871d615dbb40a6638dbb7375911ef5921cfb986298a251c251eba25ca0448dec980f05692bacab94310dd7253cf400227c80ad'
            '47382fe6431ae179f991980b2bf2f4d4ad56bfba70873659c3a7720de2fca90a60fa6033fb4cfe313e87d771312c0c80feb1617a64885362763f4b66e62543b8'
            '7449221c2aa0033d04b4d14453c3963cbac86b7820868372f7b4e11f6ba80a069f7db91ea2f8a356b0e7c1ff4c71bba406f852e37036d9349811ef52068153d7'
            '0191d609f0444ce7e120effedf890579455b8094f987f0a47bcecc0581caa7c7030799f600d9f7b705d857b7990c7eb2bba64705b1277538db9206a50bcc97c2')
package() {
  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 ksetwallpaper.py         $pkgdir/usr/share/endeavouros/scripts/ksetwallpaper.py
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname

  install -Dm755 eos-set-background-picture     $pkgdir/usr/bin/eos-set-background-picture
  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies
  install -Dm755 calamares-update               $pkgdir/usr/bin/calamares-update
  install -Dm755 welcome-dnd                    $pkgdir/usr/bin/welcome-dnd

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop      # no --enable

  install -d $pkgdir/usr/share/applications
  cp -a $pkgname.desktop $pkgname.desktop-enable
  sed -i $pkgname.desktop-enable \
      -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
      -e 's|^#\(Exec=.* --enable.*\)$|\1|'
  install -Dm644 $pkgname.desktop-enable         $pkgdir/usr/share/applications/$pkgname.desktop      # has --enable
}
