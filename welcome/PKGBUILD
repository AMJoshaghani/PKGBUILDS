# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=3.42
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-quickstart
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
)

url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

source=(
  $_url/$pkgname
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
  $_url/arm-eos-welcome-installer
)
sha512sums=('2c67b3c90acc29d8268902aee402cee0d71b1ffc6644d54e48d1fffe8e2cd75f789366568471d72e39bdde92472d18e91fcf87a8d6d276840c6cfb863bef459e'
            'fc360cf63894772bbc75f41d5b5f312ac4e70afb43bc081b63e3d8db6b8c9a1f0e08fa5a3fdff840f8064c883dda1d3f16328bfc8aa5b4a18e8adf83b93174a0'
            'd7c90688093b46195ce75a5337934890a23c3ee0cb9c58670a53ce8846fe8731473617f10ea4c735ebab26413b67d8587a003862e2af93401f88e98e92b01540'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            'ec0e3b36c5933328bc8eefac15ab0fe22081c66ecf60155a2e3b0eaa783fbec150d6e40aa5bf6da3fc0bbd8f4fbb82a789edd372952dd96d78379d9c335a2eb3'
            'e59f15450f0d7e87c2c3db41531be9e008c4dabaae23c4ee73a6575893696eba9e83d4b16f5f503537b0f498e8467bc29858ebf775d035ca737b58b6ecd6e2aa')
package() {
  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname

  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies
  install -Dm755 welcome-dnd                    $pkgdir/usr/bin/welcome-dnd
  install -Dm755 eos-install-mode-run-calamares $pkgdir/usr/bin/eos-install-mode-run-calamares
  install -Dm755 arm-eos-welcome-installer      $pkgdir/usr/bin/arm-eos-welcome-installer

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop      # no --once

  install -d $pkgdir/usr/share/applications
  cp -a $pkgname.desktop $pkgname.desktop-enable
  sed -i $pkgname.desktop-enable \
      -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
      -e 's|^#\(Exec=.* --once.*\)$|\1|'
  install -Dm644 $pkgname.desktop-enable         $pkgdir/usr/share/applications/$pkgname.desktop      # has --once
}
