# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=2.2.31
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash yad reflector-simple systemd
  python-dbus           # for ksetwallpaper.py
  xorg-xrandr           # for eos-set-background-picture
  endeavouros-theming   # for eos-set-background-picture
)
backup=(etc/eos-script-lib-yad.conf)
url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

source=(
  $url/$pkgname
  $url/$pkgname.desktop
#  $url/${pkgname}-panel.desktop
  $url/eos-script-lib-yad
  $url/eos-script-lib-yad.conf
  $url/device-info
  $url/eos-set-background-picture
  $url/ksetwallpaper.py
  $url/wallpaper-once
  $url/wallpaper-once.desktop
  $url/eos-kill-yad-zombies
  $url/translations-${pkgname}.bash
  $url/translations-${pkgname}-de.bash
  $url/translations-${pkgname}-en.bash
  $url/translations-${pkgname}-fi.bash
)
sha512sums=('71172fa88811409eebf8a4b0159291c684956d7782bff987c7a163f4d8de795e304a51089177a0b44279795fbdbb18e8a36f320febaa1b5e6244137d85a6203d'
            'd5ad5c9fe382f1614094e6677b406c059a3b6efbb56e8c8f6dcb2aa64e0bcc158fcab1b511951d0b360b07b1a491b0c36763abbb848474f2c22f71c8ec7f15e1'
            '2a4f2798cfbc95d615740dbd92cca182c66629027861aca37ae1b23d14850402118802a824d670be0eb665a1a17f385e47445bfbd0be36a20f31c31a246dcab6'
            'a65ebb01507f8fcef46052d282d32f8461d80b6e62799cbf1a4affb3188679f7df8a705b93c612e2655336343cc6411802ff613c22dd66b7bce8ceb1849d3ea2'
            '525e80b44e04d6b17d0659c8b187811dbbf39beba6b0767ca7b76d75adec569d5e7959dfa9acc34c9a602d2dae05fb3d960954fb9b5cc9a4c6e301f4bc9acbf4'
            '057bfa118a305fe7af3f8ebddcc7d1efe3d4b9bf26f61e7d3f1e756102dbfe639dea33381da363f51e1fbb12e597cbcd6ba3f43f378e3412514e2a717e92772a'
            'a3488dab4b65f215ee1513c5ff4c81b1d9e43346abb3a1e9571271c8e60e13184bd826a6206938c14c004fa5adb4f629ea832a535a51972657adb2de5bc30634'
            '4e62a6dbfac155c652e7f5fae307a75ea1648f88ec231adf03b7956ac0318306fc3c9d4def4eb42448d8a53732e2f23cf00692bbab932cee6a1ee7111e4f8d2b'
            'a5d8391ea7de40847a05a7f301871d615dbb40a6638dbb7375911ef5921cfb986298a251c251eba25ca0448dec980f05692bacab94310dd7253cf400227c80ad'
            '94395d17c28456a26edc1f4859cc382970a3a75d8c257e50721a25cceb8224a9019c43372af447f9c6800fda4f2a519e2797a4d9ad764fb736ac6e411e8d4a46'
            'db0556fbc3db88d5b5f75744fc3dc85b4f4b448b5d804d5b2d6b5489e464a24f3e54a09bdcf6d1e13ddba53fa343d6c30cddb89fb6d3238228e1a40a278f67d7'
            'e057b7dd2eedc0bb3dd04a8e3be25e8ca3eea9a857f5cce3c283b58800a0f6ea69418757f8684fca8f4e45a275cf77ee32ed917cf2900deb848d54760ef00c01'
            '4952ab738c0e2a8a7777477a04d4a0f4ec31c8234fb8bc35ffa12e5879870a58d3c140d0cfa123f97808d033e40864dd65b0ef6a1432ccb4bd5e7083641ed1fa'
            '3fa1cc3629495a218013d85da4283001fab5b2180001d94dbd1c9ecd61c40d89d48b111d7297bf921cfb7db9b2ef43fa444921243e418f1c664eb88de19c12d6')

package() {
  local xx
  cd $srcdir

  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
#  install -Dm755 ${pkgname}-panel.desktop $pkgdir/usr/share/endeavouros/scripts/${pkgname}-panel.desktop
  install -Dm755 ksetwallpaper.py         $pkgdir/usr/share/endeavouros/scripts/ksetwallpaper.py
  install -Dm644 eos-script-lib-yad       $pkgdir/usr/share/endeavouros/scripts/eos-script-lib-yad
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -Dm644 translations-${pkgname}.bash        $pkgdir/usr/share/endeavouros/scripts/translations-${pkgname}.bash
  for xx in de en fi ; do
    install -Dm644 translations-${pkgname}-$xx.bash  $pkgdir/usr/share/endeavouros/scripts/translations-${pkgname}-$xx.bash
  done

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname
  install device-info                           $pkgdir/usr/bin/device-info
  install -Dm755 eos-set-background-picture     $pkgdir/usr/bin/eos-set-background-picture
  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 eos-script-lib-yad.conf  $pkgdir/etc/eos-script-lib-yad.conf

  install -d $pkgdir/usr/share/applications
  install -Dm644 $pkgname.desktop         $pkgdir/usr/share/applications/$pkgname.desktop
}
