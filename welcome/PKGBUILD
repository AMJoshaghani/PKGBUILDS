# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=2.0.2
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash yad reflector-simple systemd
  python-dbus           # for ksetwallpaper.py
  xorg-xrandr           # for eos-set-background-picture
  endeavouros-theming   # for eos-set-background-picture
)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

source=(
  $url/$pkgname
  $url/$pkgname.desktop
  $url/eos-script-lib-yad
  $url/eos-script-lib-yad.conf
  $url/device-info
  $url/eos-set-background-picture
  $url/ksetwallpaper.py
  $url/wallpaper-once
  $url/wallpaper-once.desktop
  $url/eos-kill-yad-zombies
)
sha512sums=('8566fd90d478fe4ef567da65e9bb0a2266fac702440c2ce0c5b03bd3f1311f0b2acec405a37464db5881643a18cbf5ba2bc8f5fc26a455f6eac4b2a78243133f'
            '6831dad3957c0d4c027190b8cd048f19140fc563d384fc1cc2af66dcf29e33e779889340b7f2a6699c9aba14ff546a2766091941694093a084a95dcfa1acf60b'
            '30729b193eefc6dc18f210ff2cdae531351e8cc6884c435471e1cee7f6c83dca108e49e9189a066976140da16b285423107ae8c8752f485fe41fdad9b4bd768f'
            'e3313448a51744e25f368a1d18ea8a90edf43c2c1f80872848342e82df925e2e3e4d0a300ea2654fadc6b055f22cf6e38f99e58a9f0d3dd8e0f583d23c1c2994'
            '525e80b44e04d6b17d0659c8b187811dbbf39beba6b0767ca7b76d75adec569d5e7959dfa9acc34c9a602d2dae05fb3d960954fb9b5cc9a4c6e301f4bc9acbf4'
            '057bfa118a305fe7af3f8ebddcc7d1efe3d4b9bf26f61e7d3f1e756102dbfe639dea33381da363f51e1fbb12e597cbcd6ba3f43f378e3412514e2a717e92772a'
            '618ac5e0c681da0e0b8b59fbea215bc7fde70c5ae43d717aa0b16490172a17f5e8f507b386f7b398f02b3ce57c2ae3ada156557316060e6808fce633529ebf92'
            '4e62a6dbfac155c652e7f5fae307a75ea1648f88ec231adf03b7956ac0318306fc3c9d4def4eb42448d8a53732e2f23cf00692bbab932cee6a1ee7111e4f8d2b'
            'a5d8391ea7de40847a05a7f301871d615dbb40a6638dbb7375911ef5921cfb986298a251c251eba25ca0448dec980f05692bacab94310dd7253cf400227c80ad'
            'd5d2876231269961340dcb3607d47215e74f8cd058b81def65f1dbf811bcca780af65189874e831de6e223de4d224f416ec669f1a88e58734e7df9990342b5ee')

package() {
  cd $srcdir

  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm755 ksetwallpaper.py         $pkgdir/usr/share/endeavouros/scripts/ksetwallpaper.py
  install -Dm644 eos-script-lib-yad       $pkgdir/usr/share/endeavouros/scripts/eos-script-lib-yad
  install -Dm755 wallpaper-once           $pkgdir/usr/share/endeavouros/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname
  install device-info                           $pkgdir/usr/bin/device-info
  install -Dm755 eos-set-background-picture     $pkgdir/usr/bin/eos-set-background-picture
  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 eos-script-lib-yad.conf  $pkgdir/etc/eos-script-lib-yad.conf
}
