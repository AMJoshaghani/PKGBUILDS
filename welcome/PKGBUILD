# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=1.8
pkgrel=6
arch=('any')
license=('GPL')
depends=(bash yad reflector)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"

source=(
  $url/$pkgname
  $url/$pkgname.desktop
  $url/eos-script-lib-yad
  $url/reflector-simple
)
sha512sums=('a2a09c83ca68cdd9364feb7b88a54b1cf3954c7c796b006195092b112466599aa8f0213639ddded75cee468e1a4d9dd1553e23a0529a859117f083cd20997bff'
            '87a44d71b2d084aad1f9f3b66481c4cb54de147231fa0f10674e8d62904e5ec4bc096d077501794d0ea73db36f45122a84fb1f7233d260f0e4d4a37a2f85bb85'
            '391e0e8cd415765c70087d896f679f049b81de9d87632ab81540815a675956b1a9fb18eb64cc61f1e392707456dbbb5f256e222eed0900fa01a3d5c41d017cdb'
            'c270779a3254cfac40cade74cb23c8d8e13a7a79f352ec15fcbefffd178325800f00664bb7513682e3af7ee8e5c0e9d79d3448d55790af432978bc4a8164253e')

package() {
  cd $srcdir

  install -d $pkgdir/usr/share/endeavouros/scripts
  install -Dm755 $pkgname            $pkgdir/usr/share/endeavouros/scripts/$pkgname
  install -Dm644 eos-script-lib-yad  $pkgdir/usr/share/endeavouros/scripts/eos-script-lib-yad
  install -Dm644 $pkgname.desktop    $pkgdir/etc/xdg/autostart/$pkgname.desktop

  install -d $pkgdir/usr/bin
  ln -s /usr/share/endeavouros/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname
  install -Dm755 reflector-simple               $pkgdir/usr/bin/reflector-simple
}
