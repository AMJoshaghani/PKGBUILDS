# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.0.6
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('0948771704ac9b86ad1011f3e2790e74f7d1e25b78780d89388627a59a827a9105ea87ffd1cbc1acc77e32b62a2335cc64f9e04706d58dd8a317dab734fa2622'
            'a15e64a00549632dcaa6aea19a0252e6068c1dd92917526d7cd9e46b1f477039bbfe542f135a16784da26dd98d80367b98e22fb97db61e4baca274c82a080500'
            'e22c507e626247ce7a9217f69a06057ec45aa3e41e64f859ddabd28632a45b24504a670c703eb3f041de2accb4bb523e5fd9fea0db74327d0108356cc39bef84'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'c5354e06e7c339a08462875ffb9673ef7610bd0352c01cc042d87ea43392d97e2576a847835535b35ee145dc676af8fc861b9aa3a0f2b583fa20bda432e1b37e'
            '81005b3fb9b462f6908e03e97d0751b1383471e3ef0d43735218131c053a41296c7a4d87ae96969193081af8b389d91e08af06a1be476bf4826b69dba2875918')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
