# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('eb2c3f5a4f7e758df49d5a8dd2c929495780c67140a54a0f76d4796aeddbfab9bdd4117dc4f0fec835015d93e4db4fcc4268502075c1e9cec20201d3df99e5fb'
            'ea6f2134f4e7d2d041c02da9afee9cf1f131395da6561aa5cfe35b8e08b7ce4a07eef18b6d36177e742ac0c2cadfad73fb4d662ccdabfa079a0ff4a260f9a501'
            '9aa4ababb2930bd8dcaf591e20accc9ff3519bd9b32db346b13cee2d75a32a61cb14cba27a5a543084a1fb84cf4dc8ba426a6204fbf38b4b66f53fe10f72bb1f'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'ea4eabd57386cc887a89d11791cce6ceb17b20906764c3ace748c68867b392512ae3d8f6eb8203f03e9f93e316320775e31fa81a840558ca1af7452804f9c94f'
            '1e79078dbed2354408343f38f85063a56c31f1725b70675b4cb09ddcc50443ecd2a6333ee6fe1d19aa318bb65b3c2d998a4a0eb54670fe20516ac827ca42cfd8')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
