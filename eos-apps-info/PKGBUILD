# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.1
pkgrel=2
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('928a28f1b754ef708b4d05fd8a6eafd5bd7949594d695be238984ec42ba48b282cd28352ac832bf20df6ef35f51c98d94bf5df0d70b065c87f695a596ed1ceea'
            'cdd633a54073f072fee9598d7211b90586ca3d1807ca68026d24d15b71aa199eab1d5cfeea7168b3f9f67c96479b86b981a0392f3fe35c2b899baa2f903a4b5d'
            'e22c507e626247ce7a9217f69a06057ec45aa3e41e64f859ddabd28632a45b24504a670c703eb3f041de2accb4bb523e5fd9fea0db74327d0108356cc39bef84'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'c5354e06e7c339a08462875ffb9673ef7610bd0352c01cc042d87ea43392d97e2576a847835535b35ee145dc676af8fc861b9aa3a0f2b583fa20bda432e1b37e'
            'f6c4683d3b35f4791f7656c5a6f1e8680e6a1cce7be1222a92f93bc8dbfafe89183466dc5b3499689c3c21e1690478caacad30183234571fd64aa2cbb25a35fe')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
