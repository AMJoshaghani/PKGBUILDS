# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.0.9
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('928a28f1b754ef708b4d05fd8a6eafd5bd7949594d695be238984ec42ba48b282cd28352ac832bf20df6ef35f51c98d94bf5df0d70b065c87f695a596ed1ceea'
            '6b1746f53de18169bf23f7c2eab243ef9eb9ecbace03ed641a761849b78bbc09fce721935155a7ffd2ebc09050c4f4d2776d1f375224b4f09bd5f298b3645562'
            'e22c507e626247ce7a9217f69a06057ec45aa3e41e64f859ddabd28632a45b24504a670c703eb3f041de2accb4bb523e5fd9fea0db74327d0108356cc39bef84'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'c5354e06e7c339a08462875ffb9673ef7610bd0352c01cc042d87ea43392d97e2576a847835535b35ee145dc676af8fc861b9aa3a0f2b583fa20bda432e1b37e'
            '7ea0d1064b7202f1dbdd29262114c4014385f2870b0b1c7836b11fecf54a2d10506e3cb8bd12f2870ad3b59a5579cf8b13c17cfb78e660529f7ee75abd106b0a')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
