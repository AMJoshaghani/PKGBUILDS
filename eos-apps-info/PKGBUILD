# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.1.2
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('0d388eb5232a3c8d8c6dfe75d6788f432630455eca6cc302a4c3dc40c664e80b95f96f125b6539fd9c829997fcf0e86b2c1d9192878ef3a84748e1051bec15df'
            '03960ec9347a18a661436c75e047c61e97504415d1386fe5c4a5d0ffd9a4e6e2d460fe2fd4ccae934b368368486bcb1665e62e98d7cbd3db31f06df413fb89d0'
            '59fd2cad19572e4d4f73577ff74f37c21cd983a8e9ff35434624878cf04177be2a9964f2440aa1357abb1c45d408529fec0d2f1fa8bfc81b206b7c89f76de8f9'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'dd0a526b458c737a771077e16fb443c825536a554df6ba46e34f76008b0e043346a50a4562ba06f41a44979e22ef6f45578a9c09d207cafdc3f22fb3c98f66a2'
            'f422b7553ac7ee0398e386c8e07ddbb297e0a7136e1a30bc3f2a4d7a5f5c4e8c6b9f28f43e4deb70332620decc3a668024ea6c9e37481c2dc33ba551508fe6f3')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
