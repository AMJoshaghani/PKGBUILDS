# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.4
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  eos-bash-shared
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('a86bfb4009319cff635f6c2aad7e1b97565da54e0e5a67b5db02d2129d4f97f5b4b97ff267c91df18a19121d5e47e381e4f7c7ab88f95def33d277854c194447'
            'fed2a51653f25e0dedd1c11e54c551b0c555e8f11b32863f56005b6f0596150c79b2d344b7c99f9b05a6b110a13c6486fba0779e349a3ae6d15b5d652d595a32'
            'ee202bd96b1eaef3149eabe5b3176f312181c8b49ad68878617131da8533d66be3e456454dce65680a3652841f71078d7bb9c76633f793290c336afc73cd9436'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            '4b5eef3ce297cfa66d38b6ad92739eee905050228f72bd2c58d7f05700269b0ef434b0aa5c6ff8423a98af33918089f1255858bd2385ef3db2967d71e0d1d350'
            '1be98e9b6e7f11929dc7b772a149f17ecb6c88295b2547edf963e96b45c45189a4d57f99dd9f53cfbc69d3602483d1c62ac987d8f2f49576cdf0c8c6fce8209f')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
