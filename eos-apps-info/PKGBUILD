# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.0.1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('eb2c3f5a4f7e758df49d5a8dd2c929495780c67140a54a0f76d4796aeddbfab9bdd4117dc4f0fec835015d93e4db4fcc4268502075c1e9cec20201d3df99e5fb'
            'ea6f2134f4e7d2d041c02da9afee9cf1f131395da6561aa5cfe35b8e08b7ce4a07eef18b6d36177e742ac0c2cadfad73fb4d662ccdabfa079a0ff4a260f9a501'
            '2ed576d050a4334808f4fa808d5d67864152a32e13f1e2b013f4d122c267db252367369b3f82a59110af82a7b2080ec6da6b9d6a093698a0aba587b2568de30d'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'ea4eabd57386cc887a89d11791cce6ceb17b20906764c3ace748c68867b392512ae3d8f6eb8203f03e9f93e316320775e31fa81a840558ca1af7452804f9c94f'
            '4b86d24b5e300210ece913e04dae464a2ebeffc3ebe22bcb61282c73237ada7ed443a5abbb0fffe39a416b625eb93d72de334efa5f48259f7c136ec26b9625ca')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
