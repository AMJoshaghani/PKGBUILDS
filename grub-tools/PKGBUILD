# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=grub-tools
pkgdesc="Fixes, additions and enhancements to grub and os-prober."
pkgver=1.6
pkgrel=3
arch=('any')
license=('GPL')
depends=(grub lsb-release)
optdepends=(os-prober)

url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

source=(
  $_url/grub-update-after-kernel.hook    # Automatically updates grub.cfg after a kernel is installed/uninstalled.
  $_url/grub-fix-initrd-generation       # Command modifies os-prober and grub to fix 'initrd' line generation;
                                         # re-run if grub or os-prober is updated (but not fixed upstream!).
  $_url/grub-fix-initrd-generation.hook  # Calls the fixer after grub or os-prober upgrades.
)
sha512sums=('2062e58ae2c903187d98c05df6986d2a1a400fa9c35ffc5f91e31d3da0e779edf0b3ba21404769295dc34138ca9f6d94f12830ab3e203a4386571b05d346b4b6'
            'a6aa60d4df9b1f75f41d7c37116a0d935709a0de86e9cf1e5acbf33b75f4f074dbbbe1e5617a20da4e0437bcad5fecbf0af3283be92fcd17f5431c3a1ae6b017'
            '36ebcbc507039a2307e7bde3ee4012cc977578b47f02850c0f40a85a804cd23a98a50169e054b3b38beceb90ad99b08aef4015a7c6791287264ab8f16e3cd88b')

package() {
  cd $srcdir

  install -d $pkgdir/etc/pacman.d/hooks
  install -Dm644 grub-update-after-kernel.hook   $pkgdir/etc/pacman.d/hooks/grub-update-after-kernel.hook
  install -Dm644 grub-fix-initrd-generation.hook $pkgdir/etc/pacman.d/hooks/grub-fix-initrd-generation.hook

  install -d $pkgdir/usr/bin
  install -Dm755 grub-fix-initrd-generation    $pkgdir/usr/bin/grub-fix-initrd-generation
}
