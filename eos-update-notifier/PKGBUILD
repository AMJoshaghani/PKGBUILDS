# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-update-notifier
pkgdesc="Software update notifier and 'news for you' for EndeavourOS users."
pkgver=1.13.1
pkgrel=3
arch=('any')
license=('GPL')
depends=(bash eos-bash-shared pacman-contrib wget xdg-utils yad)
optdepends=(
  "yay:   alternative AUR helper supported by $pkgname"
  "paru:  alternative AUR helper supported by $pkgname"
  "nano:  alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
  "emacs: alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
  "vim:   alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
)
backup=(etc/$pkgname.conf)

url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

source=(
  $_url/$pkgname
  $_url/${pkgname}-configure
  $_url/$pkgname.completion
  $_url/$pkgname.conf
  $_url/arch-news-for-you
  $_url/eos-arch-news
  $_url/$pkgname.desktop
  $_url/${pkgname}-configure.desktop
  $_url/$pkgname.timer
  $_url/$pkgname.service
  $_url/checkupdatesext
)
sha512sums=('87765baa3edb2740986fb797985e879c0d357869018e64bad7c0f2daf96336e08206ed3db4202a242e7c35327eac64ffa6725a17d7d284f569f4e2bd5971d4a7'
            '65d2ca29772e82883f7ddc8e2655b40045a1847ceeedd41b3afdbeab2c01f2b8ffaeb306b6ad3449bf435eeff28e7f291658b7c141d970add46810dd5f9a6b15'
            'd5d4d92a9fd0d7a4425bb7000d208d717846ff30c2c31abac94c88840d3e8edc4862a6aacbb576330ff9d7644e63cf90e509caad629d9ed516f378e62b26e168'
            'eef733c631dcb238939bd68ca434750e5f4366903ef42d55a2df11c5dd6569538b14779847fe32557abfe40850c2cdfb5d2ccd484ca17029572f3094a5d5d065'
            '46b8363fc32276fbba0fc98ec95076b6db190622e9feca281e4d05f85b2249a4653c1e7d6779a96bb242f2fa30016d61b8b5f76ac0beb032041ce39fe70f874f'
            'e37528ac3d5c55c9adefff2daf1f199b4a95d4061a81881ce5b09d6dbc1c7131636fafdd852c6b7a5e0e6788f6a657360a1cb52a5672fd37a2ded4661071109a'
            '9c4ffa22888e1f2dbe48bfbbead655ec627d0209b52bf9bfad38cb2f58e2252db7e9dccdc1fd16485cfe8315822816ad32469f185ab325289fec72e5f6d4bd9c'
            'de931de13e9c77a1a0ec5cf3dfea25bcb79b6fd5f32aefba7292bfeaf49ff14301c2dcc0640673874b76dc59fd7579ed3153f4fc6dabb4fe5ce083bc208d7f0f'
            '47dc0b096cebee56e056c6c6996268b5495b70e027922abfcf2b5d003d2988f2cc80a63c00fae893eb916cfe28623795bd2264ddd212b0faf576179ea9ab3a35'
            'd046e675f98a52b9c310e970c5a0b0bc92b08b60d55b9d39140516977e8a6ceb4b3f9d9b05d12fce377b5744f4563e5e66a86ceecafb57f5bd486f968dc57234'
            '74903e0d815f888c480e515fffd8966b8641f47b28a76c4f4ff58067ec39310278bb8b6a193f82ca1a09271c65c216623d1421da86495111710ff813adedd27b')

package() {
  install -Dm755 $pkgname                      $pkgdir/usr/bin/$pkgname
  install -Dm755 ${pkgname}-configure          $pkgdir/usr/bin/${pkgname}-configure
  install -Dm644 $pkgname.conf                 $pkgdir/etc/$pkgname.conf
  install -Dm755 arch-news-for-you             $pkgdir/usr/bin/arch-news-for-you
  install -Dm755 eos-arch-news                 $pkgdir/usr/bin/eos-arch-news
  install -Dm755 checkupdatesext               $pkgdir/usr/bin/checkupdatesext
  install -Dm644 $pkgname.desktop              $pkgdir/etc/xdg/autostart/$pkgname.desktop

  install -Dm644 ${pkgname}-configure.desktop  $pkgdir/usr/share/applications/${pkgname}-configure.desktop

  install -d $pkgdir/usr/share/bash-completion/completions
  install -Dm644 $pkgname.completion $pkgdir/usr/share/bash-completion/completions/$pkgname
}
