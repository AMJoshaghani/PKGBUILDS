# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=eos-rankmirrors
pkgver=2.2.6
pkgrel=1
pkgdesc="EndeavourOS mirror ranking tool"
arch=('any')

# url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

license=(GPL3)
depends=(eos-bash-shared)
options=(!strip !emptydirs)
backup=(etc/$pkgname.conf)

source=(
  $url/$pkgname
  $url/eos-rankmirrors-from-hook
  $url/eos-rankmirrors.hook
  $url/eos-rankmirrors.conf
)
sha512sums=('3be24d099f9a638b0af7d7602f99213a2a48cc253b0f33b803d62056e9c92e083c122f8b7e54ab44e9a1146ef7a96cee1ad5c4744ef05f82903ec947a46ef9cc'
            '580511c676cf56b5b8580d6819e0d1a4c85a40a3cd10f4b4f04fb9c004b14025b56cf4c3048e0fe8f9eef48d924b32b97d1e9669fac39b96f4203e2d68e3bd07'
            'f01f6267f624992febfec29c76f2de1c503693086ade5783e2b9cbef359f5155bf6df520ff06299193c842aef2a05cac5078977ee0dabf55cff66f44aec6e44a'
            '9d5f981fa9f6cfca7909d5b7e484fed55d7a32452412318f10478c5bac24d81231a639370ec200c9c7a0de8d35ebba8e1286e04bd7ed95a67047855b1d4df9a7')

package() {
  local hooks=$pkgdir/usr/share/libalpm/hooks

  install -Dm644 eos-rankmirrors.hook      $hooks/eos-rankmirrors.hook

  install -Dm644 eos-rankmirrors.conf      $pkgdir/etc/eos-rankmirrors.conf

  install -Dm755 $pkgname                  $pkgdir/usr/bin/$pkgname
  install -Dm755 eos-rankmirrors-from-hook $pkgdir/usr/bin/eos-rankmirrors-from-hook
}
