# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_default
pkgver=22.03.1.10
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files and branding'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('7cd070b412781f92b169f352f60e7a37896808b29a14c3c54f1d0242d8626123449d836ca6ac60f239113143bca6a316caf784ce347ada65aa782d160081422a')

_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/endeavouros/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    cp "$srcdir/${_repo_name}/calamares/files/netinstall.yaml"  "$srcdir/${_repo_name}/calamares/modules/"
    rm "$srcdir/${_repo_name}/calamares/settings_community.conf"
    rm "$srcdir/${_repo_name}/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/${_repo_name}/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
