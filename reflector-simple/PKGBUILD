# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=reflector-simple
pkgdesc="Simple GUI wrapper for 'reflector'."
pkgver=1.16.7
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  reflector whois geoip bind-tools
  eos-bash-shared
)
optdepends=(
  'reflector-bash-completion: useful for plain reflector usage'
)
backup=(
  etc/$pkgname.conf
  etc/${pkgname}-tool.conf
)

#url=https://github.com/endeavouros-team/PKGBUILDS
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

source=(
  $_url/$pkgname
  $_url/$pkgname.desktop
  $_url/$pkgname.conf
  $_url/${pkgname}-tool.conf
  $_url/mirrorlist-rank-info
)
sha512sums=('93931fa0930c03b28d475daf4fc5adf335ce04f2ebdafb66782474f47d44206ae10062f4a8e430d610ea53fcf500ec357e5aaa451ee2a04647066ec0f151fc28'
            'bed925140e7ff3e7093b8ac150e7aa8bcd398e0a72a9ec1ef412046d3ff01e0a817ada097f76595ffb5daef5980c1df4530ac534dac0f2f23d831984b888a196'
            'e55434fae58ba393396b3b74c5615e03d84cfac29f65c6201b3de9a9bd50c53b312c4c76a06a8a58faa2ad146f6dbb003ecb901a7eba72775b58e9a5cdec60e0'
            '9c209d38732d40f92254f3eaa3cde2efdb0fc60ebb8a9f2709b2d37f110062775962db6b179a27c52fff0d77c968032856a870931d6990f784f4f464fa606685'
            '69b26dd80adf698ef339f6b9fd9e74a0074245962bd7031aba559b82211740ff8d3297aa43d20ffbfb542d2288baf7107c7a41c773bf0b5a1722f69dffe09b6e')

package() {
  cd $srcdir

  sed -i $pkgname -e "s|local VERSION_INFO=.*$|local VERSION_INFO=${pkgver}-$pkgrel|"

  install -d $pkgdir/usr/bin
  install -d $pkgdir/etc
  install -d $pkgdir/usr/share/applications

  install -Dm755 $pkgname              $pkgdir/usr/bin/$pkgname
  install -Dm755 mirrorlist-rank-info  $pkgdir/usr/bin/mirrorlist-rank-info
  
  install -Dm644 $pkgname.conf           $pkgdir/etc/$pkgname.conf
  install -Dm644 ${pkgname}-tool.conf    $pkgdir/etc/${pkgname}-tool.conf
  install -Dm644 $pkgname.desktop        $pkgdir/usr/share/applications/$pkgname.desktop
}
