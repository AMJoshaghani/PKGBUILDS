# Maintainer: EndeavourOS <info@endeavouros.com>
pkgname=endeavouros-theming
pkgver=11.0
pkgrel=1
pkgdesc='Default EndeavourOS background images and icons for all different DE'
arch=('any')
replaces=('EndeavourOS-theming')
url='https://www.endeavouros.com'
license=('GPL3')
makedepends=('git')
source=("https://github.com/endeavouros-team/endeavouros-theming/archive/refs/tags/${pkgver}.tar.gz")
md5sums=('0f32f283f7bc615e61ef6ad056905249')
sha512sums=('7b7d7b52057795825d602c004a7b2e30f42fe08e8813753f65b8011e6eda7a607c36d4a66a3b42b17c0495bdb82c22b5051d5e5043dba16ede0146025b38e198')
install=$pkgname.install

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "${pkgdir}/usr/share/endeavouros"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -d "${pkgdir}/usr/share/endeavouros/backgrounds"

  install -Dm644 *.png *.svg           "${pkgdir}/usr/share/endeavouros"
  install -Dm644 endeavouros.{svg,png} "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-icon.png  "${pkgdir}/usr/share/pixmaps"
  
  cd backgrounds
  install -Dm644 *.png                 "${pkgdir}/usr/share/endeavouros/backgrounds"

  # make symlinks for KDE:
  cd "${pkgdir}/usr/share/pixmaps"
  ln -s endeavouros.png endeavouros-logo.png
  ln -s endeavouros.svg endeavouros-logo.svg

  # cleanup
  cd $srcdir
  rm -rf ../${pkgname}
}
